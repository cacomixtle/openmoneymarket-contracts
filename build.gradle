buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'foundation.icon:gradle-javaee-plugin:0.8.0'
    }
}

subprojects {
    repositories {
        mavenCentral()
    }

    apply plugin: 'java'
    apply plugin: 'foundation.icon.javaee'

    sourceSets {
        intTest {
            compileClasspath += sourceSets.main.output
            runtimeClasspath += sourceSets.main.output
            java {
            }
        }

    }

    configurations {
        intTestImplementation.extendsFrom testImplementation
        intTestAnnotationProcessor.extendsFrom testAnnotationProcessor
        intTestRuntimeOnly.extendsFrom testRuntimeOnly
    }


    dependencies {
        compileOnly 'foundation.icon:javaee-api:0.9.0'
        implementation 'foundation.icon:javaee-scorex:0.5.2'
        implementation 'com.github.sink772:minimal-json:0.9.6'
        annotationProcessor "foundation.icon:javaee-score-client:0.9.0"

        // Use JUnit Jupiter for testing.
        testImplementation "foundation.icon:javaee-score-client:0.9.0"
        testAnnotationProcessor "foundation.icon:javaee-score-client:0.9.0"
        testImplementation 'foundation.icon:javaee-unittest:0.9.2'
        testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
        testImplementation 'org.mockito:mockito-core:4.3.1'
        testImplementation 'foundation.icon:icon-sdk:2.0.0'
        testImplementation("com.fasterxml.jackson.core:jackson-databind:2.9.6")
        testImplementation group: 'org.json', name: 'json', version: '20211205'


    }

    java {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    // need to add this option to retrieve formal parameter names
    compileJava {
        options.compilerArgs += ['-parameters']
    }


    def loadProperties = {
        def prefix = 'score-test.'
        def props = project.getProperties()
        def scoreTest = new HashMap<String, Object>()

        scoreTest.put('keystoreName', props.get(prefix + 'keystoreName'))
        scoreTest.put('keystorePass', props.get(prefix + 'keystorePass'))
        scoreTest.put('url', props.getOrDefault(prefix + 'url', 'http://localhost:9082/api/v3'))
        scoreTest.put('nid', props.getOrDefault(prefix + 'nid', 3))

        ext['scoreTest'] = scoreTest
    }
    loadProperties()
}